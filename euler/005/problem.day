import "euler/helpers.day"


fun get_prime_factor_polynomial_degree (upper_bound: int prime: int) int is
	return (cast
		int
		(floor
			(div
				(log upper_bound)
				(log prime))))
end

fun construct_polynomial_term (upper_bound: int prime: int) int is
	return (pow
		prime
		(get_prime_factor_polynomial_degree upper_bound prime))
end

fun _recursive_multiply (upper_bound: int arr: array<int> len: size_t index: size_t acc: int) int is
	match
		(gte
			index
			len) => return acc
		(default) => return (_recursive_multiply
			upper_bound
			arr
			len
			(add index 1)
			(mul
				acc
				(construct_polynomial_term
					upper_bound
					(unsafe_index arr index))))
	end
end

fun recursive_multiply (upper_bound: int arr: array<int> len: size_t) int is
	return (_recursive_multiply upper_bound arr len 0 1)
end

fun _problem005 () int is
	let primes: array<int> <- [
		2
		3
		5
		7
		11
		13
		17
		19]
	
	return (recursive_multiply
		20
		primes
		(div
			(sizeof primes)
			(sizeof int)))
end

fun problem005 () int is
	(print_problem_welcome 5)
	let solution: int <- (_problem005)
	(print_problem_solution solution 232792560)
	return 0
end
