importc "inttypes.h"
importc "math.h"

fun welcome (problem_number: int) void is
	(printf "Solving Euler problem #%03d\n" problem_number)
end

fun check_solution (solution: int64_t correct_answer: int64_t) void is
	let padding: int <- 20
	(printf
		(string_interpolate
			"Found Solution: %*"
			(string_interpolate
				PRId64
				(string_interpolate
					"\nCorrect Answer: %*"
					(string_interpolate
						PRId64
						"\n\n"))))
		padding
		solution
		padding
		correct_answer)
	(assert (eq solution correct_answer))
end

fun partial_sum (n: int scale: int) int is
	let limit: int <- (div (sub n 1) scale)
	return (div
		(mul scale
			(mul limit
				(add limit 1)
				)
			)
			2)
end

fun not_implemented () void is (exit 1) end

fun _is_prime (num: int64_t factor: int64_t) bool is
	let square_root: int64_t <- (sqrt num)
	match
		(gt factor square_root) => return true
		(eq
			0
			(mod num factor)) => return false
		(default) => return (_is_prime
			num
			(add factor 1))
	end
end

fun is_prime (num: int64_t) bool is
	return (_is_prime num 2)
end
